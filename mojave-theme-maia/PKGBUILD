# Maintainer: Jacob Birkett <ved.ttekrib@bocaj>
# Contriburor: Vince Liuice <moc.liamtoh@eciuilecniv>

# shellcheck shell=bash
# shellcheck disable=SC2034,SC2154,SC2115

pkgbase='mojave-theme-maia'
pkgname=(
    "$pkgbase-light"
    "$pkgbase-light-solid"
    "$pkgbase-light-alt"
    "$pkgbase-light-solid-alt"
    "$pkgbase-dark"
    "$pkgbase-dark-solid"
    "$pkgbase-dark-alt"
    "$pkgbase-dark-solid-alt"
    "$pkgbase-blackish"
    "$pkgbase-blackish-solid"
    "$pkgbase-blackish-alt"
    "$pkgbase-blackish-solid-alt"
)
pkgver=r452.b4d36a6
pkgrel=1
pkgdesc="A spin of Vince Liuice's Mojave theme for GTK with Manjaro Maia colors and a Blackish variant."
arch=('any')
url='https://github.com/vinceliuice/Mojave-gtk-theme'
license=('GPL3')
depends=()
makedepends=('git' 'meson')
optdepends=(
    'mcmojave-cursors: matching cursor theme'
    'mcmojave-circle-icon-theme: matching icon theme'
)
_clonedir="${url##*/}"
source=("$_clonedir::git+$url.git")
sha256sums=('SKIP')

_INSTALL_DIR='/usr/share/themes'

_THEME_DEFAULT_SMALL_VARIANT='standard'
_THEME_DEFAULT_ICON_VARIANT='manjaro'
_THEME_REPLACE_COLOR='red'

_ACCENT_COLOR_FROM='#ED5F5D'
_ACCENT_COLOR_TO='#16A085'

pkgver() {
    cd "$srcdir/$_clonedir" || exit 1
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    [[ -d "$srcdir/$pkgbase" ]] && rm -rf "$srcdir/$pkgbase"
    cp -r "$srcdir/$_clonedir/" "$srcdir/$pkgbase"

    # shellcheck disable=SC2016
    {
        local patch_from='#! */usr/bin/env/\?'
        local patch_to='#!/usr/bin/env'
        find "$srcdir/$pkgbase" -type f -name '*.sh' -exec \
            sed -i "s;$patch_from;$patch_to;" {} \;

        patch_from='${SRC_DIR}/other/plank/${name}${color}/'
        patch_to='${SRC_DIR}/other/plank/Mojave${color}/'
        sed -i "s;$patch_from;$patch_to;" "$srcdir/$pkgbase/install.sh"

        patch_from='\($ASRC_DIR/\|dest:-\)gtk-3.0'
        patch_to='\1gtk'
        sed -i "s;$patch_from;$patch_to;g" "$srcdir/$pkgbase/render-assets.sh"

        patch_from='${dest:-\(gtk\|cinnamon\)}'
        patch_to='${dest:-\1/thumbnails}'
        sed -i "s;$patch_from;$patch_to;g" "$srcdir/$pkgbase/render-assets.sh"
    }

    _BUILDS_DEST_DIR="$srcdir/build"

    [[ -d "$_BUILDS_DEST_DIR" ]] && rm -rf "$_BUILDS_DEST_DIR"
    mkdir -p "$_BUILDS_DEST_DIR"
}

_build_themes() {
    local args=(
        --dest "$_THEME_DEST_DIR"
        --name "$_THEME_BASE_NAME"
    )

    for color in "${_THEME_COLOR_VARIANTS[@]}"; do
        args+=(--color "$color")
    done
    for opacity in "${_THEME_OPACITY_VARIANTS[@]}"; do
        args+=(--opacity "$opacity")
    done
    for alt in "${_THEME_ALT_VARIANTS[@]}"; do
        args+=(--alt "$alt")
    done
    for small in "${_THEME_SMALL_VARIANTS[@]}"; do
        args+=(--small "$small")
    done
    for theme in "${_THEME_THEME_VARIANTS[@]}"; do
        args+=(--theme "$theme")
    done
    for icon in "${_THEME_ICON_VARIANTS[@]}"; do
        args+=(--icon "$icon")
    done

    "$_THEME_SOURCE_DIR/install.sh" "${args[@]}"
}

_fixup_themes() (
    for color in "${_THEME_COLOR_VARIANTS[@]}"; do
        for opacity in "${_THEME_OPACITY_VARIANTS[@]}"; do
            for alt in "${_THEME_ALT_VARIANTS[@]}"; do
                for small in "${_THEME_SMALL_VARIANTS[@]}"; do
                    for theme in "${_THEME_THEME_VARIANTS[@]}"; do
                        format_name() (
                            local name="$_THEME_BASE_NAME"

                            [[ ! "$*" =~ (^|[[:space:]])'color'($|[[:space:]]) ]] &&
                                name+="-$color"
                            [[ ! "$*" =~ (^|[[:space:]])'opacity'($|[[:space:]]) ]] &&
                                [[ "$opacity" != 'standard' ]] &&
                                name+="-$opacity"
                            [[ ! "$*" =~ (^|[[:space:]])'alt'($|[[:space:]]) ]] &&
                                [[ "$alt" != 'standard' ]] &&
                                name+="-$alt"
                            [[ ! "$*" =~ (^|[[:space:]])'small'($|[[:space:]]) ]] &&
                                [[ "$small" != 'standard' ]] &&
                                name+="-$small"
                            [[ ! "$*" =~ (^|[[:space:]])'theme'($|[[:space:]]) ]] &&
                                [[ "$theme" != 'standard' ]] &&
                                name+="-$theme"

                            echo "$name"
                        )

                        build_name="$(format_name)"
                        theme_name="$(format_name "$@")"

                        mv "$_THEME_DEST_DIR/$build_name/" "$_BUILDS_DEST_DIR/$theme_name"
                        sed -i "s;$build_name;$theme_name;" "$_BUILDS_DEST_DIR/$theme_name/index.theme"
                    done
                done
            done
        done
    done
)

build() {
    find "$srcdir/$pkgbase" -type f \( -name '*.scss' -o -name '*.svg' \) -exec \
        sed -i "s;$_ACCENT_COLOR_FROM;$_ACCENT_COLOR_TO;gI" {} \;

    cd "$srcdir/$pkgbase" || exit 1

    "$srcdir/$pkgbase/parse-sass.sh"
    "$srcdir/$pkgbase/render-assets.sh"

    [[ -d "$srcdir/$pkgbase-blackish" ]] && rm -rf "$srcdir/$pkgbase-blackish"
    cp -r "$srcdir/$pkgbase/" "$srcdir/$pkgbase-blackish"

    _THEME_SMALL_VARIANTS=("$_THEME_DEFAULT_SMALL_VARIANT")
    _THEME_THEME_VARIANTS=("$_THEME_REPLACE_COLOR")
    _THEME_ICON_VARIANTS=("$_THEME_DEFAULT_ICON_VARIANT")

    _THEME_SOURCE_DIR="$srcdir/$pkgbase"
    _THEME_DEST_DIR="$srcdir/$pkgbase/build"
    _THEME_BASE_NAME="$pkgbase"

    _THEME_COLOR_VARIANTS=('light' 'dark')
    _THEME_OPACITY_VARIANTS=('standard' 'solid')
    _THEME_ALT_VARIANTS=('standard' 'alt')

    _build_themes
    _fixup_themes 'theme'

    _THEME_SOURCE_DIR="$srcdir/$pkgbase-blackish"
    _THEME_DEST_DIR="$srcdir/$pkgbase-blackish/build"
    _THEME_BASE_NAME="$pkgbase-blackish"

    _THEME_COLOR_VARIANTS=('dark')

    _build_themes
    _fixup_themes 'color' 'theme'
}

package_mojave-theme-maia-light() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-light/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-light-solid() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-light-solid/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-light-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-light-alt/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-light-solid-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-light-solid-alt/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-dark() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-dark/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-dark-solid() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-dark-solid/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-dark-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-dark-alt/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-dark-solid-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-dark-solid-alt/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-blackish() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-blackish/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-blackish-solid() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-blackish-solid/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-blackish-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-blackish-alt/" "$pkgdir/$_INSTALL_DIR"
}

package_mojave-theme-maia-blackish-solid-alt() {
    install -d "$pkgdir/$_INSTALL_DIR"
    cp -r "$srcdir/build/$pkgbase-blackish-solid-alt/" "$pkgdir/$_INSTALL_DIR"
}
