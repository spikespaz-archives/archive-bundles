#!/bin/bash
# Maintainer: Jacob Birkett <jacob@birkett.dev>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>
# Contributer: Nico <d3sox at protonmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
# Based on:
#   https://aur.archlinux.org/packages/whitesur-gtk-theme
#   https://aur.archlinux.org/packages/whitesur-kde-theme-git

_pkgname='WhiteSur-gtk-theme'
_pkgver='2021-12-28'

pkgname=whitesur-gtk-theme
pkgver="${_pkgver//-/.}"
pkgrel=1
pkgdesc="A macOS BigSur-like theme for your GTK apps"
arch=('any')
url="https://github.com/vinceliuice/WhiteSur-gtk-theme"
license=('MIT')
makedepends=('sassc' 'which')
optdepends=(
  'gtk-engine-murrine: GTK2 theme support'
  'gtk-engines: GTK2 theme support'
  'whitesur-icon-theme: matching icon theme'
  'whitesur-cursor-theme: matching cursor theme'
)
conflicts=("${pkgname}-git")
source=(
  "${pkgname}-${_pkgver}.tar.gz::${url}/archive/${_pkgver}.tar.gz"
)
sha256sums=(
  'ea35444396b28aef8c105175c02d1ee75b89f040351bfc31d738bf35e8452c3b'
)

package() {
  cd "${_pkgname}-${_pkgver}"
  install -d "${pkgdir}/usr/share/themes"

  # See for more customization options:
  # https://github.com/vinceliuice/WhiteSur-gtk-theme#theres-so-many-customizations-you-can-do
  ./install.sh \
    -c dark \
    -t green \
    -i manjaro \
    --dest "${pkgdir}/usr/share/themes"

  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
